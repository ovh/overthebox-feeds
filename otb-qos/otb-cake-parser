#!/bin/sh
# TODO: Find a better way to do this
awk -F '[ ]{2,}' '
BEGIN {
        position="header"
        line_nb=0
        queue_nb=0
}
{
        # The line title is right after the capacity line
        if (position == "header" && $1 ~ /capacity/ ) {
                position="title"
        }
        else if (position == "title") {
                # Start matchine $2 because $1 is an empty string
                for (i = 2; i <= NF; i++) {
                        queues[i-2] = $i
                        queue_nb = queue_nb + 1
                }
                position="body"
        }
        else if (position == "body") {
                # We should match the number of queues plus 2 ( one for the
                # first empty characters, one for the title )
                if (NF != queue_nb +2) {
                        continue
                }
                # Store the line name
                lines[line_nb]=$2
                for (i = 3; i <= NF; i++) {
                        STAT[i-3,line_nb] = $i
                }
                line_nb = line_nb +1
        }
}
END {
        printf "{"
        first=1
        for (q=0; q< queue_nb; q++) {
                if (!first) {
                        printf ","
                } else {
                        first = 0
                }
                printf "\"%s\":{", queues[q]
                ffirst = 1
                for (i=0; i< line_nb; i++) {
                        if (!ffirst) {
                                printf ","
                        } else {
                                ffirst = 0
                        }
                        printf "\"%s\":\"%s\"", lines[i], STAT[q,i]
                }
                printf "}"
        }
        printf "}"
}'
